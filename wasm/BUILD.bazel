# Bazel build
# C/C++ documentation: https://docs.bazel.build/versions/master/be/c-cpp.html

load("//bazel:ray.bzl", "COPTS")
load("@rules_rust//rust:defs.bzl", "rust_binary", "rust_dylib_library", "rust_library")
load("@rules_rust//cargo:defs.bzl", "cargo_build_script")
load("@rules_foreign_cc//foreign_cc:defs.bzl", "cmake")

cmake(
    name = "libwavm-static",
    build_args = ["-j8"],
    cache_entries = {
        "WAVM_ENABLE_STATIC_LINKING": "ON",
    },
    env = {
        "V": "1",
        "VERBOSE": "1",
    },
    lib_source = "@com_github_wavm_wavm//:all_srcs",
    out_static_libs = [
        "libWAVM.a",
        "libWAVMBLAKE2.a",
        "libWAVMUnwind.a",
        "libWAVMlmdb.a",
    ],
)

cargo_build_script(
    name = "ray_wasm_buildrs",
    srcs = ["build.rs"],
    build_script_env = {
        "PROTOC": "$(execpath @com_google_protobuf//:protoc)",
        "PROTO_SRCS": "$(locations //src/ray/protobuf:gcs_proto_file)",
    },
    data = [
        "//:ray_common_headers",
        "//src/ray/protobuf",
        "//src/ray/protobuf:common_proto_file",
        "//src/ray/protobuf:gcs_proto_file",
        "@io_opencensus_proto//opencensus/proto/metrics/v1:metrics_proto",
    ],
    rustc_env = {
    },
    tools = [
        "@com_google_protobuf//:protoc",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "@wasm_crate_index//:prost-build",
    ],
)

genrule(
    name = "llvm_flags_file",
    outs = ["llvm_flags"],
    cmd = "llvm-config --libs --ldflags > $@",
)

rust_library(
    name = "ray_wasm_lib",
    srcs = glob(
        [
            "src/**/*.rs",
        ],
        exclude = [
            "src/cmd/*.rs",
        ],
    ),
    crate_name = "wasm_on_ray",
    crate_root = "src/lib.rs",
    rustc_flags = [
        "@$(location llvm_flags_file)",
        # "-L/usr/lib/llvm-10/lib",
        # "-lLLVM-10",
        #"$$(llvm-config --libs --ldflags)",
    ],
    visibility = ["//visibility:public"],
    data = [
        "llvm_flags_file",
    ],
    deps = [
        "libwavm-static",
        "ray_wasm_buildrs",
        "//:core_worker_lib",
        "//:global_state_accessor_lib",
        "@wasm_crate_index//:anyhow",
        "@wasm_crate_index//:clap",
        "@wasm_crate_index//:lazy_static",
        "@wasm_crate_index//:libc",
        "@wasm_crate_index//:prost",
        "@wasm_crate_index//:rmp",
        "@wasm_crate_index//:serde",
        "@wasm_crate_index//:serde_json",
        "@wasm_crate_index//:sha256",
        "@wasm_crate_index//:tokio",
        "@wasm_crate_index//:tracing",
        "@wasm_crate_index//:tracing-subscriber",
        "@wasm_crate_index//:uuid",
        "@wasm_crate_index//:wasmtime",
    ],
)

# this rule is trying to use shared library.
# but it doesn't work on Mac right now due to
# issues with the rust libstd.
rust_dylib_library(
    name = "ray_wasm_lib.dylib",
    srcs = glob(
        [
            "src/**/*.rs",
        ],
        exclude = [
            "src/cmd/*.rs",
        ],
    ),
    crate_name = "wasm_on_ray",
    crate_root = "src/lib.rs",
    rustc_flags = [
        "-C",
        "prefer-dynamic",
        "-C",
        "link-arg=-lunwind",
    ],
    visibility = ["//visibility:public"],
    deps = [
        "libwavm-static",
        "ray_wasm_buildrs",
        "//:core_worker_lib",
        "//:global_state_accessor_lib",
        "@wasm_crate_index//:anyhow",
        "@wasm_crate_index//:clap",
        "@wasm_crate_index//:lazy_static",
        "@wasm_crate_index//:libc",
        "@wasm_crate_index//:prost",
        "@wasm_crate_index//:rmp",
        "@wasm_crate_index//:serde",
        "@wasm_crate_index//:serde_json",
        "@wasm_crate_index//:sha256",
        "@wasm_crate_index//:tokio",
        "@wasm_crate_index//:tracing",
        "@wasm_crate_index//:tracing-subscriber",
        "@wasm_crate_index//:uuid",
        "@wasm_crate_index//:wasmtime",
    ],
)

cc_binary(
    name = "libray_wasm.so",
    copts = COPTS,
    linkopts = select({
        "@bazel_tools//src/conditions:darwin": [
            #TODO: Hide symbols on Mac.
        ],
        "@bazel_tools//src/conditions:windows": [
            #TODO: Hide symbols on Windows.
        ],
        "//conditions:default": [
            #TODO: Hide symbols on Linux.
        ],
    }),
    linkshared = 1,
    linkstatic = 1,
    visibility = ["//visibility:public"],
    deps = [
        "ray_wasm_lib",
    ],
)

rust_binary(
    name = "wasm_worker",
    srcs = glob([
        "src/cmd/wasm_worker.rs",
    ]),
    crate_name = "wasm_worker",
    crate_root = "src/cmd/wasm_worker.rs",
    visibility = ["//visibility:public"],
    deps = [
        ":ray_wasm_lib",
        "@wasm_crate_index//:anyhow",
        "@wasm_crate_index//:clap",
        "@wasm_crate_index//:lazy_static",
        "@wasm_crate_index//:prost",
        "@wasm_crate_index//:serde",
        "@wasm_crate_index//:serde_json",
        "@wasm_crate_index//:sha256",
        "@wasm_crate_index//:tokio",
        "@wasm_crate_index//:tracing",
        "@wasm_crate_index//:tracing-subscriber",
        "@wasm_crate_index//:uuid",
    ],
)

rust_binary(
    name = "raywa",
    srcs = glob([
        "src/cmd/raywa.rs",
    ]),
    crate_name = "raywa",
    crate_root = "src/cmd/raywa.rs",
    visibility = ["//visibility:public"],
    deps = [
        #":ray_wasm_lib.dylib",
        ":ray_wasm_lib",
        "@wasm_crate_index//:sha256",
        "@com_google_absl//absl/time",
        "@wasm_crate_index//:anyhow",
        "@wasm_crate_index//:clap",
        "@wasm_crate_index//:lazy_static",
        "@wasm_crate_index//:tokio",
        "@wasm_crate_index//:tracing",
        "@wasm_crate_index//:tracing-subscriber",
        "@wasm_crate_index//:libc",
    ],
)

genrule(
    name = "ray_wasm_pkg",
    srcs = [
        "wasm_worker",
        "raywa",
        "libray_wasm.so",
        "ray_wasm_lib.dylib",
        "libwavm-static",
    ],
    outs = ["ray_wasm_pkg.out"],
    cmd = """
        WORK_DIR="$$(pwd)" &&
        PY_WASM_DIR="$$WORK_DIR/python/ray/wasm" &&
        rm -rf $$PY_WASM_DIR && 
        mkdir -p "$$PY_WASM_DIR/lib/" &&
        cp -f $(location wasm_worker) "$$PY_WASM_DIR/" &&
        cp -f $(location libray_wasm.so) "$$PY_WASM_DIR/lib/" &&
        cp -f -r $(locations libwavm-static) "$$PY_WASM_DIR/lib/" &&
        cp -f $(location ray_wasm_lib.dylib) "$$PY_WASM_DIR/lib/" &&
        cp -f $(location raywa) "$$PY_WASM_DIR/" &&
        echo "$$WORK_DIR" > $@
    """,
    local = True,
    visibility = ["//visibility:public"],
)
